plugins {
    id 'com.android.library'
    id 'maven-publish'
}

//get git Hash
def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 1
        versionName "1.2.1"
        versionNameSuffix = "-"+getGitHash()+"-stable"
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "supportlib-${variant.name}-v${defaultConfig.versionName}.aar"
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //gms play-services
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.1'
}

//publishing stuff
//repo version
def getVersionName = { ->
    return "1.2.1"
}

def getArtifactId = { ->
    return "lib-release.aar"
}

def getGroupId = { ->
    return "com.linusba.support"
}

def getGitHubUrl = { ->
    return "https://maven.pkg.github.com/mbudget0x01/supportlib"
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId getGroupId()
                artifactId getArtifactId()
                version getVersionName()
                artifact(sourceJar)
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"

                url = uri(getGitHubUrl())
                credentials {
                    username = System.getenv("PUBLISH_USER")
                    password = System.getenv("PUBLISH_TOKEN")
                }
            }
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}


